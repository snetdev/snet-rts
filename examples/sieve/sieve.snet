<metadata><default> <interface value="C4SNet" /> </default></metadata>

net sieve ({<N>,<M>} -> {<X>} )
{
  net init
  connect [{<N>,<M>} -> if <N >= 2 && M >= N>
                        then {<P=2>,<Q>} ; {<A=3>,<N>,<M>}
                        else ];

  net iter ({<A>,<N>,<M>} -> {<I>,<N>})
  {
    net test ({<A>,<N>,<M>} -> {<I>,<N>} | {<A>,<N>,<M>})
    connect [{<A>,<N>,<M>} -> if <A <= M>
                              then {<I=A>,<N>} ; {<A=A+1>,<N>,<M>}
                              else {<I=0>,<N>} ];
  }
  connect test * {<I>,<N>};

  net sieve ({<P>,<Q>} | {<I>,<N>} -> {<X>})
  {
    net filt ({<I>,<N>} | {<P>,<Q>} -> {<I>,<N>,<Z>} |
                                       {<P>,<Q>,<Z>} |
                                       {<X>,<Z>})
    {
      net sync
      connect [| {<P>,<Q>}, {<I>,<N>} |];

      net test ({<P>,<Q>,<I>,<N>} -> {<P>,<Q>} |
                                     {<I>,<N>,<Z>} |
                                     {<P>,<Q>,<Z>} |
                                     {<X>,<Z>})
      connect [{<P>,<Q>,<I>,<N>} -> if <I == 0 && Q == 0> then
                                    else if <I == 0>
                                    then {<I>,<N>,<Z>}
                                    else if <I % P == 0>
                                    then {<P>,<Q>} 
                                    else if <Q == 0 && I <= N>
                                    then {<P=I>,<Q=0>,<Z>} ;
                                         {<P>,<Q=Q+1>}
                                    else if <Q == 0>
                                    then {<X=I>,<Z>} ; {<P>,<Q>}
                                    else {<I>,<N>,<Z>} ; {<P>,<Q>}];
    }
    connect (sync .. (test | [])) * {<Z>};

    net unzet
    connect [{<Z>} -> {}] | [];
  }
  connect (filt .. unzet) * {<X>};
}
connect init .. (iter | []) .. sieve;

