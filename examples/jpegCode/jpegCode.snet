<metadata>
  <boxdefault>
    <interface value="C4SNet" />
  </boxdefault>
</metadata>
net jpegCode ({bmpfilename, jpgfilename} -> {<stop>})
{
       box openBMP ( (bmpfilename, jpgfilename) -> (rows, cols,infile, outfile));
       box get_MB ((rows, cols,infile, outfile) -> (count, total_ele, width, height, lastMatrix, outfile, infile, <firstTime>)| (YMatrix, color, row, col, sample) | (CrMatrix,color, row, col, sample) | (CbMatrix, color, row, col, sample));

       box dct ((Matrix, color, row, col, sample) -> (dctresult, color, row, col, sample));
       box zzq_encode ( (dctresult, color, row, col, sample) -> (bitstream, color, row, col, sample));

       box saveResult ( (count, total_ele, width, height, lastMatrix, outfile, infile, bitstream, color, row, col, sample, <firstTime>)-> (count, total_ele, width, height, lastMatrix, outfile, infile, <firstTime>) | ( <stop>));
}
connect 
	openBMP .. get_MB ..
	(   
	     (  
		      ([{YMatrix, color} -> {Matrix = YMatrix,  color}] .. dct .. zzq_encode )
		   || ([{CrMatrix,color} -> {Matrix = CrMatrix, color}] .. dct .. zzq_encode )
                  || ([{CbMatrix,color} -> {Matrix = CbMatrix, color}] .. dct .. zzq_encode )
        		
    	      )
	   || []
	)
	.. 
	(
		([| {count, total_ele, width, height, lastMatrix, outfile, infile,<firstTime>}, {bitstream, color, row, col, sample}|])
	       ..  (saveResult | [])
	)**{<stop>};




