<metadata><default>
    <interface value="C4SNet" />
</default></metadata>

net loadbalance ({conf} -> {output})
{
  box init ((conf) -> (state) | (collect) | (probe, <node>));

  net estimate ({probe, <node>} -> {<node>})
  {
    box estimator ((probe, <node>) -> (<node>));
  }
  connect estimator !@ <node>;

  net loop ({state} | {<node>} -> {result})
  {
    net sync ({state} | {<node>} -> {state, <node>})
    connect [| {state}, {<node>} |] * {state, <node>};

    box inbox ((state, <node>) -> (state) | (work, <node>));

    net process
    {
      box worker ((work, <node>) -> (result) | (<node>));
    }
    connect [] | worker !@ <node>;
  }
  connect (sync .. inbox .. process) \ {state}, {<node>};

  net post ({collect} | {result} -> {output})
  {
    net sync ({collect} | {result} -> {collect, result})
    connect [| {collect}, {result} |] * {collect, result};

    box merge ((collect, result) -> (collect) | (output));
  }
  connect (sync .. merge) \ {collect}, {result};
}
connect init .. (estimate | []) .. ([] | loop) .. post;

