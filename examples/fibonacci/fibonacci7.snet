<metadata> <default> <interface value="C4SNet" /> </default> </metadata>

net fibonacci7 ({<N>} -> {<X>})
{
    net addlevel
    connect [{<N>} -> {<N>,<L=0>,<M=1>}];

    net recur
    {
        net leq1
        connect [{<N>} -> if <N <= 1> then {<N>,<T>} else {<N>,<F>}];

        net true
        connect [{<N>,<T>} -> {<R=N>}];

        net false
        connect [{<N>,<F>,<M>,<L>} -> {<N=N-1>,<L=L+1>,<M=(2*M)+1>}
                                    ; {<N=N-2>,<L=L+1>,<M=(2*M)+0>}];
    }
    connect (leq1 .. (false || true)) ** {<R>};

    net merge ({<M>,<L>,<R>} -> {<X>})
    {
        net rewrite
        connect [{<L>,<M>,<R>} -> if <L <= 0> then {<X=R>} else
                                  if <M % 2 == 0> then {<L=L>, <M=M/2>, <V=R>}
                                  else {<L=L>, <M=M/2>, <W=R>}];
        net pair
        {
            net sync
            {
                net cells
                {
                    net cell
                    connect [| {<V>}, {<W>} |];
                }
                connect cell*{<V>,<W>};
            }
            connect cells!!<L>;

            net sum
            connect [{<V>,<W>,<L>} -> {<R=V+W>,<L=L-1>}];
        }
        connect sync .. sum;

        net Xpass
        connect [{<X>} -> {<X>}];
    }
    connect (rewrite .. (Xpass || pair)) \ {<M>,<L>};

}
connect addlevel .. recur .. merge;
